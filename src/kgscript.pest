topcode = {
    SOI ~ code ~ EOI
}

code = {
    command ~ (newline ~ command)* ~ newline?
}

command = {
    ns_def | fn_def | assignment | for_loop | while_loop | expression | nothing
}

nothing = { "" }

ns_def = {
    "ns" ~ name ~ block0
}

fn_def = {
    "fn" ~ name ~ param_list? ~ block0
}

for_loop = {
    "for" ~ name ~ "in" ~ expression ~ block0
}

while_loop = {
    "while" ~ expression ~ block0
}

if_expr = {
    "if" ~ expression ~ block0
    ~ ("elif" ~ expression ~ block0)*
    ~ ("else" ~ block0)?
}

raw_name = @{
    (ASCII_ALPHA | "_")
    ~ (ASCII_ALPHANUMERIC | "_")*
}

name = {
    !keyword ~ raw_name
}

param_list = {
    "|" ~ name+
}

block0 = {
    "{" ~ code ~ "}"
}

block_with_args = {
    "@" ~ block_arglist ~ block0
}

block_arglist = {
    name ~ ("," ~ name)*
}

assignment = {
    name ~ "=" ~ expression
}

expression = {
    if_expr | block_any | expr_logical_sum
}

literal = {
    lit_string | lit_char | lit_float | lit_int | lit_bool | lit_nothing
}

lit_nothing = { "nothing" }

block_any = {
    block0 | block_with_args
}

newline = _{
    ("\n" | "\r")+
}

expr_logical_sum  = { expr_logical_prod ~ ("or"             ~ expr_logical_prod)* }
expr_logical_prod = { expr_test         ~ ("and"            ~ expr_test        )* }
expr_test         = { expr_sum          ~ (operator_test    ~ expr_sum         )* }
expr_sum          = { expr_product      ~ (operator_sum     ~ expr_product     )* }
expr_product      = { expr_call         ~ (operator_product ~ expr_call        )* }

expr_call = {
    expr_half_atom+
}

expr_half_atom = {
    unop ~ expr_half_atom
    | expr_atom
}

expr_atom = {
    literal
    | name
    | ("(" ~ expression ~ ")")
}

lit_string = @{
    "\""
    ~ (!"\"" ~ ANY)*
    ~ "\""
}

lit_char = @{
    "'"
    ~ (!"'" ~ ANY)
    ~ "'"
}

lit_int = @{
    ('0'..'9')+
}

lit_float = ${
    lit_float_part_int ~ (
        lit_float_part_frac ~ lit_float_part_exp?
        | lit_float_part_frac? ~ lit_float_part_exp
    )
}

lit_float_part_int = @{
    ('0'..'9')+
}

lit_float_part_frac = @{
    "." ~ ('0'..'9')+
}

lit_float_part_exp = @{
    "e"
    ~ ("+" | "-")?
    ~ ('0'..'9')+
}

lit_bool = @{
    "true" | "false"
}

keyword = @{
    "or" | "and" | lit_bool | "fn" | "ns" | "for" | "while" | "if" | "in" | "nothing"
}

operator_test = @{
    "==" | "<=" | ">=" | "!=" | "<" | ">"
}

operator_sum = @{
    "+" | "-"
}

operator_product = @{
    "*" | "/" | "//" | "%"
}

unop = @{
    "!"
}

COMMENT = _{
    "#"
    ~ (!newline ~ ANY)*
    ~ newline
}

WHITESPACE = _{
    " " | "\t"
}
